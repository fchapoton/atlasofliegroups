<cells.at
<springer_reductive.at
<K_highest_weights.at
<springer_tables.at

{extract just the parameters from [(int,int,Param)] (output of weak_packet(..)}
set parameters([(int,int,Param)] triples)=[Param]:for (,,p) in triples do p od

{extract just the parameters from [[(int,int,Param)]] (output of weak_packets(...)}
set parameters([[(int,int,Param)]] packet)=[Param]:
{##for triple in packet do for (,,p) in triple do p od od}
##for triple in packet do parameters(triple) od

{weak Arthur packets}

{SpringerTable: table of Springer data, see springer_tables.at
 NOTE: this must be the SpringerTable for the integral root system
 ComplexNilpotent: complex orbit for dual group
 [Param]: list of parameters, should be all_parameters_gamma(G,gamma) where
   gamma is regular and differs from gamma_target by X^*
 ratvec gamma_target: target infinitesimal character, probably O.H/2
result: [(int,int,Param)]=[(block number, cell number, parameter)]}

{weak_packet, given Springer table for the integrality datum of O_check.H/2,
 a list of parameters at regular infinitesimal character, and a target infinitesimal character}
set weak_packet(SpringerTable st_int,ComplexNilpotent O_check,[Param] params,ratvec gamma_target)=[(int,int,Param)]:
assert(integrality_datum(O_check.root_datum.dual,O_check.H/2)=st_int.root_datum,
 "root datum mismatch, probably not using the integrality datum for the orbit");
if #params=0 then [] else
let ()=if cells_verbose then
prints("gamma_target:",gamma_target);
 prints("Computing weak packet for ", params[0].real_form, new_line,
 "given # parameters: ", #params,new_line,
 "dual orbit:",new_line, " dual root datum: ", O_check.root_datum,new_line, " diagram: ", O_check.diagram, new_line,
 " normalized diagram: ", O_check.diagram_normalized, new_line,
 " H: ", O_check.H, new_line,
 " dimension: ", O_check.dim_nilpotent)
fi then
blocks_and_cells=prints("Computing blocks and cells for ",#params, " parameters");blocks_and_cells(params) then
()=if cells_verbose then
prints("computed ", #blocks_and_cells, " blocks, of sizes: ", for (b,) in blocks_and_cells do #b od)
fi  then
{ let  gamma=O_check.H/2 then}
 ()=if cells_verbose then prints("inf. char.=",gamma_target) fi then
 O=st_int.dual_map_i(O_check) then
 ()=if cells_verbose then
  prints("orbit:",new_line, " root datum: ", O.root_datum, new_line, " diagram: ", O.diagram, new_line,
   " normalized diagram: ", O.diagram_normalized, new_line,
   " H: ", O.H, new_line,
   " dimension: ", O.dim_nilpotent)
 fi then
 sigma=st_int.springer(O) then
 ()=if cells_verbose then prints("sigma=", sigma, " of dimension ",st_int.ct.dimension(sigma)) fi then
 parameters=parameters_given_AV_infinitesimal_character(st_int,O,blocks_and_cells,gamma_target) 
{ ()=if cells_verbose then
  prints("found ", #parameters, " parameters") fi}
 in
parameters
fi

{same as previous command, with additional permutation:
 for computing dual_map_i and springer(O) we do some case-by-case/table constructions
 the labeling of simple roots in the given root datum, and the case-by-case constructed
 on (using simple_factors) may be different
 permutation is the matrix taking i^th simple root of given root datum to i^th simple root
  of the constructed one
}
set weak_packet(SpringerTable st_int,ComplexNilpotent O_check,[Param] params,ratvec gamma_target,mat permutation)=[(int,int,Param)]:
if #params=0 then [] else
let ()=if cells_verbose then
prints("gamma_target:",gamma_target);
 prints("Computing weak packet for ", params[0].real_form, new_line,
 "dual orbit:",new_line, " dual root datum: ", O_check.root_datum,new_line, " diagram: ", O_check.diagram, new_line,
 " normalized diagram: ", O_check.diagram_normalized, new_line,
 " H: ", O_check.H, new_line,
 " dimension: ", O_check.dim_nilpotent)
fi then
blocks_and_cells=blocks_and_cells(params) then
()=if cells_verbose then
 prints("computed ", #blocks_and_cells, " blocks")
fi  then
{ let  gamma=O_check.H/2 then}
 ()=if cells_verbose then prints("inf. char.=",gamma_target) fi then
 O=st_int.dual_map_i(O_check) then
 ()=if cells_verbose then
  prints("orbit:",new_line, " root datum: ", O.root_datum, new_line, " diagram: ", O.diagram, new_line,
   " normalized diagram: ", O.diagram_normalized, new_line,
   " H: ", O.H, new_line,
   " dimension: ", O.dim_nilpotent)
 fi then
 ()=prints("computing springer map of", O.diagram) then
 sigma=st_int.springer(O) then
 ()=if cells_verbose then prints("sigma=", sigma, " of dimension ",st_int.ct.dimension(sigma)) fi then
 parameters=parameters_given_AV_infinitesimal_character(st_int,O,blocks_and_cells,gamma_target,permutation) 
{ ()=if cells_verbose then
  prints("found ", #parameters, " parameters") fi}
 in
parameters
fi

{call weak_packet(st_int,O_check,G,params,gamma_target):
 set params=all_paramaeters_gamma(G,O_check.H/2+2*rho(G)) and gamma_target=O.H/2
}
set weak_packet(SpringerTable st_int,ComplexNilpotent O_check,RealForm G)=[(int,int,Param)]:
if cells_verbose then
 prints("weak_packets(st_int,O_check,G):", new_line,"O_check: ", O_check.diagram ,new_line, "G: ", G) fi;
let rd=st_int.root_datum then
gamma_target=O_check.H/2 then
gamma_reg=gamma_target+2*rho(rd) then
all=all_parameters_gamma(G,gamma_reg) in
weak_packet(st_int,O_check,all,gamma_target)

{given an orbit O_check on the dual side
 gamma=O_check.H/2
 rd=integrality_datum(rd,gamma)
 return all orbits for rd_dual whose G^vee-saturation is the original
 orbit
this is entirely on the dual side
}
{this function is probably NOT the right thing to do,
 see integral_descent() below, left this here temporarily 
 for testing purposes}
set integral_descents(ComplexNilpotent O_check)=[ComplexNilpotent]:
let rd=O_check.root_datum.dual then
rdint=integrality_datum(rd,O_check.H/2) then
integral_orbits=rdint.dual.orbits then
H=O_check.H in
##for O_int_check in integral_orbits do
{H is in X_*(dual group), so
is_conjugate(H,O_int_check.H,rd.dual) is the right conjugacy test}
if is_conjugate(H,O_int_check.H,rd.dual) then [O_int_check] else [] fi od

{compute integral_descents(O_check) (on the dual side)
 rd_int=integrality datum, rd_int_check=dual side (subgroup)
 integral_descents(O_check) is a set of orbits for rd_int_check
 return their duals, which are special orbits for rd_int}
 {this function should not be used, here only for testing}
set integral_descents(SpringerTable st_int,ComplexNilpotent O_check)=
let descents=integral_descents(O_check) in
for O_check_int in descents do
dual_orbit_by_diagram(st_int,O_check_int.diagram_normalized) od

{this is the correct function to use: it returns a single 
ComplexNilpotent for the rd_int.dual
}
set integral_descent(ComplexNilpotent O_check)=ComplexNilpotent:
let rd=O_check.root_datum.dual then
rd_int=integrality_datum(rd,O_check.H/2) in
(rd_int.dual,(),O_check.H)

{this version returns the correct orbit for the given rd_int.dual}
set integral_descent(SpringerTable st_int,ComplexNilpotent O_check)=ComplexNilpotent:
let O_check_int=O_check.integral_descent in
dual_orbit_by_diagram(st_int,O_check_int.diagram_normalized)

{This is the primary weak_packet function}
{given complex orbit on dual side, and real form, compute weak Arthur packet}
{NOTE: the appropriate SpringerTable is for the integral root system}
{returns: ([Param],[(int,int,Param)],[]) for backwards compatibility}
set weak_packet(ComplexNilpotent O_check,RealForm G)=([Param],[(int,int,Param)],[SpringerTable]):
let gamma_final=O_check.H/2 then
gamma=gamma_final+2*rho(G) then  {this is the target infinitesimal character, need to pass it}
all=all_parameters_gamma(G,gamma) then
rd=O_check.root_datum.dual then
()=prints("gamma:",gamma) then
()=prints("gamma_final:",gamma_final) then
rd_int=integrality_datum(rd,gamma) then
st_int=springer_table(rd_int) then
()=prints("integral data: st_int") then
(valid,permutation)=isomorphism_permutation(st_int.root_datum,rd_int.derived) then
()=prints("rd_int:",rd_int, new_line, "st_int.rd: ", st_int.root_datum) then
()=assert(valid,"can't find isomorphism of rd_int and st_int.root_datum") then
{dual_integral_orbits=integral_descents(st_int,O_check) in}
O_check_int=integral_descent(st_int,O_check) then
()=prints("computing packet for:", O_check_int) then
wp=weak_packet(st_int,O_check_int,all,gamma_final,permutation) in
(parameters(wp),wp,[SpringerTable]:[])

{run over a set of dual orbits}
set weak_packets([ComplexNilpotent] dual_orbits,RealForm G)=([Param],[[(int,int,Param)]],[SpringerTable]):
let weak_packets=[[(int,int,Param)]]:[] in
if cells_verbose then prints("weak_packets(G) with dual orbits and G=",G) fi;
for O in dual_orbits do
 prints(new_line,"Computing weak packet for orbit: ", O.root_datum, " ", O.H, " dim=", O.dim_nilpotent);
 let (,wp,)=weak_packet(O,G) in
 weak_packets#:=wp
od;
(parameters(weak_packets),weak_packets,[SpringerTable]:[])

{given just a RealForm G, compute the dual orbits then compute
all weak packets as you run over these dual orbits}
set weak_packets(RealForm G)=([Param],[[(int,int,Param)]],[SpringerTable]):
weak_packets(G.dual_orbits,G)

{----precomputed cell trace version ----}

{weak_packet:
 given: a RootDatum rd and an even orbit O_check for the dual,
 a SpringerTable for rd,
 "cell_data": precomputed cell characters
 compute weak packet using the precomputed cell traces
} 
set weak_packets(
SpringerTable st,
ComplexNilpotent O_check,
[([Param],[([int],[int])])]  cell_data
{[Param]: list of parameters in block
 [([int] indices of parameters in cell,[int] character of cell)]
 }
)=([Param],[(int,int,Param)],[SpringerTable]):
assert(is_locally_isomorphic(integrality_datum(O_check.root_datum.dual,O_check.H/2),st.root_datum),
 "root datum mismatch, probably not using the integrality datum for the orbit");
let gamma_target=O_check.H/2 then
()=if cells_verbose then
 prints("gamma_target:",gamma_target);
 prints("Computing weak packet for ", #cell_data, " blocks ",
 "dual orbit:",new_line, " dual root datum: ", O_check.root_datum,new_line, " diagram: ", O_check.diagram, new_line,
 " normalized diagram: ", O_check.diagram_normalized, new_line,
 " H: ", O_check.H, new_line,
 " dimension: ", O_check.dim_nilpotent)
fi then
()=prints("using precomputed parameters and cells ") then
()=if cells_verbose then prints("inf. char.=",gamma_target) fi then
 O=st.dual_map_i(O_check) then
 ()=if cells_verbose then
  prints("orbit:",new_line, " root datum: ", O.root_datum, new_line, " diagram: ", O.diagram, new_line,
   " normalized diagram: ", O.diagram_normalized, new_line,
   " H: ", O.H, new_line,
   " dimension: ", O.dim_nilpotent)
fi then
sigma=st.springer(O) then
()=if cells_verbose then prints("sigma=", sigma, " of dimension ",st.ct.dimension(sigma)) fi then
wp=parameters_given_AV_infinitesimal_character(st,O,gamma_target,cell_data)
in (parameters(wp),wp,[SpringerTable]:[])

{same as previous, running over a set of (even) orbits}
set weak_packets(
SpringerTable st,[ComplexNilpotent] dual_orbits,[([Param],[([int],[int])])]  cell_data)=
let wp=for O in dual_orbits do let (,weak_packet,)= weak_packets(st,O,cell_data) in weak_packet od
in (parameters(wp),wp,[SpringerTable]:[])

{compute SpringerTable also}
set weak_packets(
RealForm G,
[ComplexNilpotent] dual_orbits,
[([Param],[([int],[int])])]  cell_data)=
weak_packets(springer_table(G),dual_orbits,cell_data)

{compute G.even_dual_orbits also}
set weak_packets(
RealForm G,
[([Param],[([int],[int])])]  cell_data)=
weak_packets(springer_table(G),G.even_dual_orbits,cell_data)

set weak_packets_pre(RealForm G)=
weak_packets(G,cell_data(character_table(G),G))

{prepare cell_data for input to weak_packets(st,O,cell_data)}
set cell_data(CharacterTable ct,[Param] parameters)=
[([Param],[([int],[int])])]:
let bc=blocks_and_cells(parameters) then
cells=[[int]]:[],
characters=[[int]]:[] in
for i:#bc do
   let (parameters,cells)=bc[i] in
   (parameters,for cell in cells do
    let (params,)=cell in 
    (params,ct.cell_character(cell))
   od)
od

set cell_data(CharacterTable ct,RealForm G)=
cell_data(ct,all_parameters_gamma(G,rho(G)))

{------/precomputed cell trace version ----}

{run over a set of dual orbits}
set weak_packets([ComplexNilpotent] dual_orbits,RealForm G)=([Param],[[(int,int,Param)]],[SpringerTable]):
let weak_packets=[[(int,int,Param)]]:[] in
if cells_verbose then prints("weak_packets(G) with dual orbits and G=",G) fi;
for O in dual_orbits do
 prints(new_line,"Computing weak packet for orbit: ", O.root_datum, " ", O.H, " dim=", O.dim_nilpotent);
 let (,wp,)=weak_packet(O,G) in
 weak_packets#:=wp
od;
(parameters(weak_packets),weak_packets,[SpringerTable]:[])





{reporting functions}

set mult([[(int,int,Param)]] packet)=((int,int,int)->int):(int orbit,int block,int cell):
let rv=0 in
for (b,c,p) in packet[orbit] do
 if b=block and c=cell then rv+:=1 fi
od;rv

set parameters_map([[(int,int,Param)]] packet)=((int,int,int)->[Param]):(int orbit,int block,int cell):
let rv=[Param]:[] in
for (b,c,p) in packet[orbit] do
 if b=block and c=cell then rv#:=p fi
od;rv

set mult_block([[(int,int,Param)]] packet)=((int,int)->int):(int orbit,int block):
let rv=0 in
for (b,c,p) in packet[orbit] do
 if b=block  then rv+:=1 fi
od;rv

set blocks([[(int,int,Param)]] packets)=sort_u(##for i:#packets do for (b,,) in packets[i] do b od od)
set cells([[(int,int,Param)]] packets) =sort_u(##for i:#packets do for (,c,) in packets[i] do c od od)

set show_detail([[(int,int,Param)]] packets)=void:
let blocks=blocks(packets) then
cells=cells(packets) then
m=mult(packets) in
tabulate(
 ["orbit","block#","cell#","#packet"]#
 ##for i:#packets do
  ##for b in blocks do
   ##for c in cells do
    if m(i,b,c)!=0 then [[i.to_string,b.to_string,c.to_string,m(i,b,c).to_string]] else [] fi 
   od
  od
 od
      #["Total","","",(#parameters(packets)).to_string]
  )

set show_very_long([[(int,int,Param)]] packets)=void:
let blocks=blocks(packets) then
cells=cells(packets) then
m=mult(packets) in
tabulate(
 ["orbit","block#","cell#","parameters", "inf. char."]#
 ##for i:#packets do
  ##for b in blocks do
   ##for c in cells do
    for p in packets.parameters_map(i,b,c) do
    [i.to_string,b.to_string,c.to_string,p.to_string, p.infinitesimal_character.to_string] 
   od
  od
 od
od
     #["Total","","",(#parameters(packets)).to_string,""]

  )

set show([[(int,int,Param)]] packets)=void:
let blocks=blocks(packets) then
cells=cells(packets) then
m=mult_block(packets) in
tabulate(
 ["orbit","block#","#packet"]#
 ##for i:#packets do
  ##for b in blocks do
    if m(i,b)!=0 then [[i.to_string,b.to_string,m(i,b).to_string]] else [] fi 
   od
  od
       #["Total","",(#parameters(packets)).to_string]

  )

set show([ComplexNilpotent] orbits,[[(int,int,Param)]] packets)=void:
let blocks=blocks(packets) then
cells=cells(packets) then
m=mult_block(packets) in
tabulate(
 ["orbit#","diagram","dim.","block#","#packet"]#
 ##for O@i in orbits do 
  ##for b in blocks do
    if m(i,b)!=0 then [[i.to_string,O.diagram_normalized.to_string,O.dim_nilpotent.to_string,b.to_string,m(i,b).to_string]] else [] fi 
   od
  od
 #["Total","","","",(#parameters(packets)).to_string]

  )
  
set show_one_block([[(int,int,Param)]] packets, int block_number)=void:
let blocks=blocks(packets) then
cells=cells(packets) then
total=0 then
m=mult_block(packets) in
tabulate(
 ["orbit","#packet"]#
 ##for i:#packets do
  ##for b@j in blocks do
    if j=block_number and m(i,b)!=0 then total+:=m(i,b); [[i.to_string,m(i,b).to_string]] else [] fi 
   od
  od
 #["Total",total.to_string]
  )

set show_short([[(int,int,Param)]] packets)=void:
let blocks=blocks(packets) then
cells=cells(packets) then
m=mult(packets) in
tabulate(
 ["orbit","#packet"]#
 ##for i:#packets do
  let total=sum(for b in blocks do sum(for c in cells do m(i,b,c) od) od)
 in if total!=0 then [[i.to_string,total.to_string]] else [] fi
od
   #["Total",(#parameters(packets)).to_string]
)


set export([Param] parameters)=void:
prints("set parameters=[");
for p@i in parameters do
 let comma=if i<#parameters-1 then "," else "" fi then
 (,n)=%p.x, lambda=p.lambda, nu=p.nu in
 prints("parameter(G,",n,",",lambda,",",nu,")",comma) od;
prints("]")


set export_parameters([[(int,int,Param)]] packets)=void:
export(parameters(packets))

{----------------more output routines---------------------------------}

set weak_packets([RealForm] list)=
##for G in list do 
 for H in real_forms(G) do weak_packets(H)
od
od

set weak_packets_all_forms(RealForm G)=weak_packets([G])

set weak_packets_all_forms_long(RealForm G)=
let rf=real_forms(G) then
m=weak_packets_all_forms(G) in
for (a,b,)@i in m do prints(rf[i]); show_short(b)
od

set weak_packets_all_forms_long([RealForm] groups)=void:
let rf=[RealForm]:[] then
()= for G in groups do rf##:=real_forms(G) od then
m=weak_packets(groups) in
prints(new_line,"Summary of weak packets:");for (a,b,)@i in m do
prints("------------------------------------------",new_line,rf[i]); show_short(b) od

