<basic.at
<ext_deform.at { for full_deform@(Param,mat) }
<thetastable.at {for dim_u_cap_p}

{    c_form on standard representations, correct in all cases   }

{ c_form_std:
  write c-invariant form on standard module in terms of standardrepks,
  as a ParamPol
}
set c_form_std (Param p) = ParamPol:
  let G=real_form(p) in
  if G.is_equal_rank
  then full_deform(p) {built-in is faster in equal rank case}
  else full_deform(p,G.distinguished_involution)
  fi

{ with explicit |delta|, always call |full_deform@(Param,mat)| }
set c_form_std = (Param,mat->ParamPol): full_deform@(Param,mat)

{    c_form on irreducible representations   }

{ a ParamPol of standardrepks giving the c-invariant form on irreducible(p)

  this is the correct formula for the c-form on G in all cases
  (it is only to compute the Hermitian form on G that we shall also need,
  when G is not equal rank) the c-form on the extended group)
}
set c_form_irreducible (Param p) = ParamPol:
( let ori_nr_p = orientation_nr(p), P = null_module(real_form(p)) in
  for c@q in KL_sum_at_s(p)
  do P +:= c*orientation_nr_term(ori_nr_p,q)*full_deform(q) od
; P
)

{ same as | c_form_irreducible|, but also return a second component that
  exposes the linear combination of contributions from standard representations
  that produced the result (parameter, coefficient, c_form on ths standard)

  formulas: write
    J(y)  =sum_x               (-1)^{ell(x)-ell(y)}P(x,y)(q=1)I(x)
  then
    J(y)_c=sum_x (-1)^{on(x,y)}(-1)^{ell(x)-ell(y)}P(x,y)(q=s)I(x)_c
  where
    P(x,y) is a cumulated KL polynomial
    (-1)^{ell(x)-ell(y)}P(x,y) is given by signed_KL_col(y)[i] with
    indices[i]=x on(x,y)=orientation number given by orientation_nr_term()
    I(x)_c given as combination of standards x' with nu(x')=0 by c_form_std(x)
  algorithm: compute the sum for J(y)_c, using signed_KL_col and c_form_std
}
set c_form_irreducible_long (Param p) = (ParamPol,[(Param,Split,ParamPol)]):
( let P = null_module(real_form(p)), ori_nr_p = orientation_nr(p)
  then standards = { list contributions; meanwhile compute |P| }
    for c@q in KL_sum_at_s(p)
    do
      let form_on_standard = c_form_std(q)
      ,   factor           = c*orientation_nr_term(ori_nr_p,q)
      in P +:= factor*form_on_standard
    ; (q,factor,form_on_standard)
    od
  in (P,standards)
)

{    c_form_irreducible_twisted and its variants   }

{ The following function implements algorithm from Vogan in email 9/22/16:
  with change: start with twisted polynomial, not ordinary
  * for each delta-fixed term q add full_deform(q,delta),
  * for each pair {q,delta(q)} add q*0 (deformation to nu=0 of q).
  For second kind it it convenient to add q*0 and delta(q)*0 separately
  and at the end divide the sum of such contributions by 2.
  It is not clear at the moment what the mathematical meaning of the expression
  is, so I'm calling it |c_form_irreducible_twisted| for now. It is what is
  needed to compute the Hermitian form on an irreducible (multiply each term
  in this sum by 1 or s; which of the two is to be determined later)
}

{ basic version of c_form_irreducible_twisted, for any delta }
set c_form_irreducible_twisted (Param p,mat delta) = ParamPol:
( let G=p.real_form, ori_nr_p = orientation_nr(p)
  then fixed_terms = null_module(G), unfixed_terms = null_module(G)
  , P=KL_sum_at_s(p), Ptw=twisted_KL_sum_at_s(p)
  in
  for c@q in P
  do
    if is_fixed(delta,q)
    then
      let new_c=half((c+Ptw[q])+(c-Ptw[q]).times_s)
      then c=new_c*orientation_nr_term(ori_nr_p,q)
      in fixed_terms +:= c*full_deform(q,delta)
    else
      let c=c*orientation_nr_term(ori_nr_p,q)
      in unfixed_terms +:= c*one_plus_s*(q*0)
    fi
  od
; fixed_terms + half(unfixed_terms)
)

{ if delta isn't given assume it is the default }
set c_form_irreducible_twisted (Param p) = ParamPol:
  c_form_irreducible_twisted(p,real_form(p).distinguished_involution)

{ included for backward compatibility: }
set c_form_irreducible_delta = (Param->ParamPol):
  c_form_irreducible_twisted@Param

{ more information about c_form_irreducible_delta }
set c_form_irreducible_twisted_long (Param p,mat delta) =
    (ParamPol,[(string,Split,Param,ParamPol)],ParamPol):
( let G=p.real_form
  , ori_nr_p = orientation_nr(p)
  then fixed_terms = null_module(G), unfixed_terms = null_module(G)
  , terms=[(string,Split,Param,ParamPol)]: []
  , P=KL_sum_at_s(p), Ptw=twisted_KL_sum_at_s(p)
  then character_formula=P.s_to_1
  in
  for c@q in P
  do
    if is_fixed(delta,q)
    then
      let new_c=half((P[q]+Ptw[q])+(P[q]-Ptw[q]).times_s)
      then contrib=new_c*orientation_nr_term(ori_nr_p,q)*full_deform(q,delta)
      in
        (fixed_terms+:=contrib, terms#:=("fixed",c,q,contrib))
    else
      let contrib=c*orientation_nr_term(ori_nr_p,q)*one_plus_s*(q*0)
      in
        (unfixed_terms+:=contrib, terms#:=("unfixed",c,q,contrib))
    fi
  od
; (character_formula,terms,fixed_terms + half(unfixed_terms))
)

{ if delta isn't given assume it is the default }
set c_form_irreducible_twisted_long (Param p) =
    (ParamPol,[(string,Split,Param,ParamPol)],ParamPol):
  c_form_irreducible_twisted_long(p,real_form(p).distinguished_involution)

{ for backwards compability: }
set c_form_irreducible_delta_long =
    (Param->ParamPol,[(string,Split,Param,ParamPol)],ParamPol):
  c_form_irreducible_twisted_long@Param

{ print versions }
set print_c_form_irreducible_twisted_long (Param p) = void:
  let (character_formula,terms,answer)=c_form_irreducible_twisted_long(p) in
  prints(new_line,"Character Formula:",character_formula,new_line);
  for (type,coeff,p,P) in terms
  do prints("coeff*standard module:", coeff,"*",p,":",new_line
     	   ,"fully deformed (", type, "):", P, new_line)
  od;
  prints("final result:", answer)

set print_c_form_irreducible_twisted_long (Param p) = void:
( let (,b,f) = c_form_irreducible_twisted_long(p) in
  for (q,coeff,,Q) in b
  do prints("")
  ; prints("standard module: ",q)
  ; prints("coefficient: ",split_format(coeff))
  ; prints("form on standard: ")
  ; pol_format(Q)
  od
; prints(new_line,"c-form on irreducible module:")
; prints(p, " at ",infinitesimal_character(p)); pol_format(f)
)

{ analyse }

{ print only terms with "mixed" coefficient (a+bs), i.e., both a,b\ne 0 }
set mixed (Split w)= bool: not w.is_pure
set analyse (ParamPol P) = void:
( prints("Mixed terms:")
; for w@p in P
  do
    if w.mixed
    then prints("(",split_format(w),")*",p,", ",infinitesimal_character(p))
    fi
  od
)


{ ---------------------------------------- }

{ commands for converting from c-form to hermitian-form}
{ Compute (lambda+rho) restricted to torus T }

{ lambda\in X^*+rho =>
    one can define a parameter with this lambda, as p=param(x,lambda-rho,0)
  then infinitesimal_character(p)=(1+theta)lambda/2
  which is lambda restricted to T-tilde
  apply this with lambda-rho=0, returns (1+theta)rho/2     (on T-tilde)
  apply this with lambda-rho,   return  (1+theta)lambda/2  (on T-tilde)
  then the sum is (1+theta)(lambda+rho)/2 on T
}
set lambda_plus_rho_res_T (KGBElt x,ratvec lambda) = ratvec:
  (1+involution(x))*(lambda+rho(real_form(x)))/2

{ mu(p) is the value of the lowest K-type of p, evaluated at x(p) }
{ formula: < (1+theta_x)(lambda+rho)/2 , torus_factor(x)+rho^\vee > }
set mu(Param p) = rat:
  lambda_plus_rho_res_T(p.x,p.lambda) * (p.x.torus_factor+p.real_form.rho_check)

set convert_cform_hermitian (ParamPol P)= ParamPol:
  if #P=0 then P { since we cannot select a term for a null polynomial }
  else
    let a_mu = mu(first_param(P))
    in null_module(real_form(P)) +
      for w@p in P do (s^(rat_as_int(mu(p)-a_mu))*w,p) od
  fi

{  converting to Hermitian forms in unequal rank case  }

{ assumption: theta_x commutes with delta
  => positive real roots are delta-invariant
  also no C- roots (this holds if x=x(p) from (finalized) parameter with nu=0)
  need number of pairs {alpha,delta(alpha)} for which alpha is positive real
  and <alpha,delta(alpha^vee)>=0
  this computes the sign of delta acting on Lambda^top(positive real roots)
  each orthgonal pair {alpha,delta(alpha)} contributes -1
  if <alpha,delta(alpha)>=-1 this contribute 1 by computation in SL(3,R)
  -1 from pair is cancelled by -1 from action on
  [X_alpha,X_delta(alpha)=X_{alpha+beta}
  if delta(alpha)=alpha contributes 1 by reduction to simple case,
  note every real-simple root is simple
}
set number_real_orthogonal_pairs (KGBElt x, mat delta) = int:
  let rd=x.root_datum, theta=x.involution in
  assert(delta*theta=theta*delta
        ,"Cartan involution does not commute with delta");
  assert(no_Cminus_roots(x)
        , "Not defined unless all complex roots are type C+");
  let real_posroots=real_posroots(rd,theta) in
  for alpha in real_posroots
  do int:
    let pairing=coroot(rd,alpha)*delta*alpha in
    assert(pairing=0 or pairing=-1 or pairing=2
          ,"invalid pairing of root and coroot");
    #=pairing { count zero pairings }
  od.sum.half

{ implementing formula from Vogan in email 10/25/16, subject line mu(p) }
set mu (Param p,mat delta) = rat:
  assert(=nu(p),"nu(p) !=0");
  let E=E(delta,p), mu_unextended=mu(p), x=x(p)
  then tau_term = E.l*(delta-1)*E.tau/4
  ,    dim_term = dim_u_cap_p(x)
  , scalar_term = number_real_orthogonal_pairs(x,delta)
  in
  prints("mu_unextended=", mu_unextended,new_line
        ,"tau_term=",tau_term,new_line
        ,"dim_term=", dim_term,new_line
        ,"scalar_term=",scalar_term);
  mu_unextended+tau_term+scalar_term

{remaining terms copied over from corresponding versions in hermitian.at}
set convert_cform_hermitian (ParamPol P,mat delta)= ParamPol:
  if =#P then P { since we cannot select a term for a null polynomial }
  else
    let a_mu = mu(P.first_param,delta) in
    P.real_form.null_module +
    for w@p in P do (s^rat_as_int(mu(p,delta)-a_mu)*w,p) od
  fi


{   Hermitian forms on irreducibles     }

{ ParamPol giving the hermitian form on irreducible p in all cases}
set hermitian_form_irreducible (Param p) = ParamPol:
  if is_equal_rank(real_form(p))
  then convert_cform_hermitian(c_form_irreducible(p))
  else convert_cform_hermitian(c_form_irreducible_twisted(p))
  fi

{ nice output of hermitian_form_irreducible }
set print_hermitian_form_irreducible (Param p,mat delta) = void:
  pol_format(hermitian_form_irreducible(p))

{ nice output of Hermitian forms on list of parameters }
set print_hermitian_form_irreducible ([Param] P,mat delta) = void:
  for p in P
  do prints(new_line,"Hermitian form on irreducible: ",p)
  ; print_hermitian_form_irreducible(p,delta)
  od

{ nice output of hermitian_form_irreducible }
set print_hermitian_form_irreducible (Param p) = void:
  pol_format(hermitian_form_irreducible(p))

{ nice output of Hermitian forms on list of parameters }
set print_hermitian_form_irreducible ([Param] P) = void:
  for p in P
  do prints(new_line,"Hermitian form on irreducible: ",p)
  ; print_hermitian_form_irreducible(p)
  od

{   unitarity and weak unitarity tests }

{ assuming P is the ParamPol of the Hermitian
  form on an irreducible, is_unitary(P) tests
  if it is unitary: <=> every Split coefficient is real,
  or every Split coefficient is nonreal
}
set is_unitary = (ParamPol->bool): is_pure@ParamPol  { from basic.at }

{ compute Hermitian form on p, and report if it is unitary }
set is_unitary (Param p) = bool: hermitian_form_irreducible(p).is_unitary

set print_is_unitary (Param p) = void:
  let P=hermitian_form_irreducible(p) in
  if P.is_unitary
  then prints("Unitary")
  else let (real,nonreal,mixed)=purity(P) in
    prints("Non-Unitary",new_line
          , real, " real terms", new_line
          , nonreal," nonreal terms", new_line
	  , mixed, " mixed terms") fi

{ no mixed terms }
set is_weakly_unitary (ParamPol P) = bool:
  let (,,mixed)=purity(P) in mixed=0

{ compute Hermitian form on p, and report if it is weakly unitary }
set is_weakly_unitary (Param p) = bool:
  let G=real_form(p) in
  if is_equal_rank(G)
  then is_weakly_unitary(c_form_irreducible(p))
  else is_weakly_unitary(c_form_irreducible_twisted(p))
  fi

{ for "big" versions: see extParamPol.at }

set test_line (Param p) = void:
( let (x,lambda_minus_rho,gamma) = %p
  , points=reducibility_points(p)
  ,   ipoints=integrality_points(root_datum(p),2*nu(p))
  in prints("testing line through ", p)
; prints("reducibility points: ",points)
; prints("integrality points (for 2*nu): ",ipoints)
; if #points=0
  then prints("entire line is irreducible and unitary"
       	     ,if is_final(p*0) then "" else " (nu=0 is not final)" fi)
  else points:=(0/1)#points { tack on initial zero }
  ; if not points[#points-1]=1/1 then points:=points#(1/1) fi
  ; for i:#points-1 from 1
    do
      let t=(points[i]+points[i-1])/2
      then q=param(x,lambda_minus_rho,gamma*t)         { mid point interval }
      ,    r=param(x,lambda_minus_rho,gamma*points[i]) { end point interval }
      in prints(nu(p)*t,": ", is_unitary(q))
      ;  prints(nu(p)*points[i],": ", is_unitary(r))
    od
  fi
)


set weak_test (Param p) = bool:
  let formula=c_form_irreducible_delta(p)
  , bad=null_module(real_form(p))
  in
  for w@p in formula do if not is_pure(w) then bad+:=w*p fi od;
  if #bad=0 then prints("number of terms: ", #formula); true
  else prints("number of terms: ", #formula,new_line,"bad terms:", bad ); false
  fi

set strong_test (Param p,mat delta)=bool:
  let formula=hermitian_form_irreducible(p)
  , bad=null_module(real_form(p))
  in
  for w@p in formula do if not is_pure(w) then bad+:=w*p fi od;
  if #bad=0 then prints("number of terms: ", #formula);true
  else prints("number of terms: ", #formula,new_line,"bad terms:", bad );false
  fi

