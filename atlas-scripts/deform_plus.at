<basic.at
<polynomial.at { for type |i_poly| and function |eval| }
<deform.at
<Weylgroup.at

set deformation_terms (Param p) = ParamPol:
  let (B,KL_mat,length) = compress(partial_KL_block(p:=normal(p))) then nB=#B in
  if =nB or B~[0]!=p
  then null_module(p) { if |p| itself is not among survivors, return 0 }
  else { compute final column |acc| of |Q| matrix by solving linear system
         $P'*acc=e_p$ with $P'$ a matrix of Split, columns from |KL_sum_at_s| }
    let remainder = null(nB-1)#1 { start with standard basis vector for |p| }
    , solution=null(nB)
    , !opposite_parity=1-length~[0]%2 { opposite to that of |length(p)| }
    , !on_p = orientation_nr(p)
    in
    for q_ind : nB
    ~do let contrib=remainder[q_ind]*KL_mat[q_ind] in
      remainder -:= contrib
    ; if length[q_ind]%2=opposite_parity
      then solution +:= contrib
      fi
    od
  ; null_module(p)+
    for c@i in solution
    do { twist (1-s)*solution according to orientation numbers of its terms }
      let x=B[i] then (q,r)=(on_p-orientation_nr(x))\%2 in
      assert(=r,"odd orientation");
      (minus_1^q*c*one_minus_s,x) { we have |s^q*c*(1-s) = (-1)^q*c(1-s)| }
      { for the same reason we replaced |s| by |-1| in computations giving |c| }
    od
  fi

{ the original version of this file started here }

set deformation (Param p) = (Param,ParamPol): { deformed Param and new terms }
  (lower(p),deform(p)) { here |deform| may be replaced by |deformation_terms| }

set recursive_deform_plus (Param p) =([Param],ParamPol):
{  if d_verbose then prints("recursive_deform with:", p) fi;}
  if not is_final(p) then error("improper parameter") fi;
  let terms_computed=ParamPol:null_module(p) then  {extra information}
  empty=null_module(real_form(p)) in
  if d_verbose { choose verbose or non-verbose version of the function }
  then rec_fun full_def (Split sc,Param p) ([Param],ParamPol):
			 { |sc| will simply scale the resulting ParamPol }
    begin
      prints("full_def with: sc=", sc, ", p=", p);
      terms_computed+:=p;  {every time full_def is called ad p to terms_computed}
      assert(is_standard(p)
	    ,"non standard parameter encountered in deformation");
      let acc=empty, at_nu0=ParamPol: sc*(p.at_nu_0) { fully deformed |p| } in
      for factor in reducibility_points(p)
      ~do
	let p_def=p*factor { next deformation of |p| } 
	then def=deformation_terms(p_def) { bypass built-in |deform| here }
        in
          prints("adding deformation term: p=", p_def, new_line,"adding:", def);
          acc+:=def
      od
    ; for k@q in acc { traverse the sum of the deformation terms spun off }
      do { apply |full_def| recursively to all those terms }
      	let (,P)=full_def(sc*k,q) in
        at_nu0+:=P; { gather harvest from those deformations }
        terms_computed+:=q  {add q to terms_computed}
      od
    ; prints("full_def complete, result:",at_nu0)
    ; (monomials(terms_computed),at_nu0)
    end
  else rec_fun full_def (Split sc,Param p) ([Param],ParamPol):
			 { |sc| will simply scale the resulting ParamPol }
    begin
      assert(is_standard(p),"non standard parameter in deformation");
      terms_computed+:=p;  {every time full_def is called ad p to terms_computed}
      let acc=empty, at_nu0=ParamPol: sc*(p.at_nu_0) { fully deformed |p| } in
      for factor in reducibility_points(p)
      ~ { direction of deformation is down, towards 0; this is not essential }
      do
      acc+:=deformation_terms(p*factor);
      terms_computed+:=p*factor
{      ;prints("terms_computed is now: ",#terms_computed)}
    od
    ; for k@q in acc { traverse the sum of the deformation terms spun off }
       do
        let (,P)= full_def(sc*k,q)  in
        at_nu0 +:= P;
        terms_computed+:=q
      od
    ;(monomials(terms_computed), at_nu0)
    end
  fi { apply that recursive function to the initial argument: } (1,p)

set chamber_rep(Param p)=Param:
parameter(p.x, p.lambda, p.nu\1 + p.root_datum.rho/(p.root_datum.coxeter_number+1))
