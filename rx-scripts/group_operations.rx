<misc
<lattice { for (co)restrict_action }
<torus { for torus }

{----------------some more general group constructions----------------}

{real forms}

{ preferred terminology when argument is not an inner class: inner_forms }

set inner_forms (RealForm G) = real_forms(InnerClass:G)

set print_real_forms(InnerClass ic)= void:
  for n@i in form_names(ic)
  do prints(i,": ",n)
  od

set print_inner_forms (RealForm G) = void:
begin print_real_forms(G)
; prints("To define a real form rf do: set rf=real_forms(G)[i]")
end

{ defining split groups }

set split_form(InnerClass ic) = { try to retain the same inner class }
  let rd = root_datum(ic)
  then ic_split = inner_class(rd,-id_mat(rank(rd)))
  in quasisplit_form(
    if distinguished_involution(ic)=distinguished_involution(ic_split)
    then ic else ic_split fi)

{ direct product of RootDatum, InnerClass and RealForm }

set *(RootDatum R,RootDatum S) = RootDatum:
  root_datum(block_matrix(simple_roots(R),simple_roots(S))
            ,block_matrix(simple_coroots(R),simple_coroots(S)))

set *(InnerClass ic1,InnerClass ic2) = InnerClass:
  let di =
    block_matrix(distinguished_involution(ic1),distinguished_involution(ic2))
  in inner_class(root_datum(ic1)*root_datum(ic2),di)

set real_form (InnerClass ic, string name) = RealForm:
  let fn = form_names(ic) then i=#fn-1
  then () = while i>=0 and fn[i]!=name do i-:=1 od
  in if i<0 then error("unknown real form") else real_form(ic,i) fi

set form_number_by_fiber_partition (InnerClass ic,vec v) = int:
  let realforms=real_forms(ic), i=nr_of_real_forms(ic)-1
  in while i>0 and fiber_partition(realforms[i])!=v do i-:=1 od; i

set * (RealForm G,RealForm H) = RealForm:
  real_form(inner_class(G)*inner_class(H), form_name(G) + "." + form_name(H))

{ then ((n,,),,,) = Cartan_info(fundamental_Cartan(G))
  { fiber part of G has size 2^number of compact factors of fundamental torus }
  then v=vec: []
  then ()=
    for i in fiber_partition(G)
    do for j in fiber_partition(H) do v:=v#(i+(2^n)*j) od od
  in real_forms(ic)[form_number_by_fiber_partition(ic,sort(v))]
}


{ ----------------radical, i.e. maximal central torus---------------- }

set radical (RootDatum rd) = RootDatum:
  let empty = null(rank(rd)-semisimple_rank(rd),0) in root_datum(empty,empty)

set radical (InnerClass ic) = RealForm:
  let rd = root_datum(ic) then ssr = semisimple_rank(rd)
  then coradical = columns_with((int j): j>=ssr,root_coradical(rd))
  then involution = restrict_action(distinguished_involution(ic),coradical)
  in quasisplit_form(inner_class(radical(rd),involution))

set maximal_central_torus = (RootDatum->RootDatum): radical@RootDatum
set maximal_central_torus = (InnerClass->RealForm): radical@InnerClass

{ -------------derived, adjoint inner class and real form--------------  }

set derived (InnerClass ic) = InnerClass:
  let (der_rd,proj)=derived_info(root_datum(ic))
  in inner_class(der_rd,corestrict_action(distinguished_involution(ic),proj))

set adjoint(InnerClass ic) = InnerClass:
  let (adj_rd,inj)=adjoint_info(root_datum(ic))
  in inner_class(adj_rd,restrict_action(distinguished_involution(ic),inj))


set derived (RealForm G) = RealForm:
  real_forms(derived(inner_class(G)))[form_number(G)]

set adjoint(RealForm G)=
  real_forms(adjoint(inner_class(G)))[form_number(G)]

{ ----------------real Levi factors---------------- }

set Levi (RootDatum rd, (int->bool) select) = RootDatum:
  root_datum(columns_with(select,simple_roots(rd))
            ,columns_with(select,simple_coroots(rd)))

set Levi (RootDatum rd,[int] S) = Levi (rd,is_member(S))

set Levi (InnerClass ic,[int] S) = InnerClass: { just change the root datum }
  inner_class(Levi(root_datum(ic),is_member(S)),distinguished_involution(ic))

{ ------------------Levi factor of real parabolic----------- }


{ M=centralizer of A in H=TA, as a RealForm }
{ don't have a definitive way to decide the real form
  use split_rank(M_der)+dim(A)=split_rank(G)
  might be more than one real form of M satisfying this }
set real_Levi(RealForm G,KGBElt x) = RealForm:
  let ic_M =
    inner_class(root_datum(simple_imaginary_subsystem(x)),involution(x))
  then C_M=Cartan_class(ic_M,0), splrt = split_rank(torus(Cartan_class(x)))
  , rv= [RealForm]: []
  then () =
    for H in real_forms(ic_M)
    do if split_rank(derived(H))+splrt = split_rank(G) then rv#:=H fi od
  ; if #rv>1
    then for H in rv do prints(H) od
    ; error("Could not determine real form of Levi factor of parabolic")
    fi
  in rv[0]

set theta_stable_Levi (RealForm G,KGBElt x) = RealForm:
  let ic_L=inner_class(root_datum(simple_real_subsystem(x)), involution(x))
  in quasisplit_form(ic_L)

set M = real_Levi@(RealForm,KGBElt) { synonym }

set L = theta_stable_Levi@(RealForm,KGBElt) { synonym }






{ ----------------tests for classes of groups---------------- }

set is_equal_rank(RealForm G) = bool:
  let ((,a,b),,,) = Cartan_info(fundamental_Cartan(G)) in a+b=0

set is_split(RealForm G) = bool:
let ((a,b,),,,) = Cartan_info(most_split_Cartan(G)) in a+b=0

set is_quasisplit (RealForm G) = bool: form_number(G)=nr_of_real_forms(G)-1
set is_quasicompact (RealForm G) = bool: form_number(G)=0

set is_semisimple (RootDatum rd) = bool: semisimple_rank(rd) = rank(rd)

set is_simply_connected (RootDatum rd) = bool:
  is_semisimple(rd) and abs(det(simple_coroots(rd)))=1

set is_adjoint (RootDatum rd) = bool:
  is_semisimple(rd) and abs(det(simple_roots(rd)))=1

set has_connected_center (RootDatum rd)=is_simply_connected(derived(dual(rd)))

{ for real groups return information about the topology of G(R) }
{ set is_simply_connected_real_group(RealForm G) = bool:
    is_simply_connected(root_datum(K_0(G)))
}



