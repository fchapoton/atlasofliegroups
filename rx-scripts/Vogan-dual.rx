<basic.rx
<parameters.rx { for dual_inner_class }
<extended_misc.rx {for shift_g_regular }

set rho_i ((RootDatum, mat) rd_theta) = ratvec:
  sum(imaginary_poscoroots(rd_theta))/2

{ half sum of positive coroots for roots not integral on (torus element of) z }
set rho_diff_integral (RootDatum rd, ratvec z) = ratvec:
  rho_check(rd)-rho(integrality_datum(dual(rd),z))

{ |rho_diff_integral(z)|, but made $\theta$-stable by a sum-of-roots shift.
  Here |x| just serves to record |theta=involution(x)|; such |x| is easy to
  build from |theta|, and |cross| is more efficient than matrix conjugation
}
set theta_stable_diff ((KGBElt, ratvec)(x,z):xz) = ratvec:
  let rd = RootDatum: real_form(x)
  then ssr1 = semisimple_rank(rd)-1
  , tsd (KGBElt x,ratvec z) = ratvec:[] { recursive stub }
  in (assert(all(for alpha in real_posroots(x) do denom(alpha*z)=1 od))
     ; tsd :=
      ((KGBElt x,ratvec z) ratvec:
       let s=ssr1 { test simple reflections of being complex descents }
       in while s>=0 and status(s,x)>0 do s-:=1 od
       ; if s<0 then let res=rho_diff_integral(rd,z)
         in assert (res*involution(x)=res,"unstable"); res
         else let rs=reflection(rd,s) in tsd(cross(s,x),z*rs)*rs
         fi
      )
     )(xz)

{
  When restricting to a centraliser subgroup, we shall need to correct a
  potential torus factor |tf| with respect to the changed system of
  imaginary roots. The final correction is computed by the |integral_shift| as
  an integer vector, which moreover should be $\theta$-stable, so that
  whatever correction is made will not change the square class that has been
  fixed beforehand; the condition for which the correction is computed is to
  get the proper grading on the simple-imaginary roots of the subsystem, which
  are passed as |simples| argument. That grading undergoes two changes. Even
  if |tf| remains valid it grades imaginary roots indirectly through an offset
  $\rho_im$; this means we must compensate for the grading change implied for
  the difference of the offset between full system and subsystem. Apart from
  that, we may have (had to) to shift |tf| in order to get into the proper
  (square class for the subsystem) coset. In order to compute the correction
  we pass (not |tf| but) that shift made, minus to old $\rho_im$ as |v|; the
  new $\rho_im$ value is implicit in |simples| and is not included in the
  passed value of |v|. So we must find an (integral) vector |w| such that for
  all roots alpha in |simples|: < w , alpha > = < v , alpha > + 1 (mod 2).
  This condition being $\theta$-invariant, we hope the solution is as well.
}
set integral_shift(ratvec v, mat simples) = vec:
  let vals = for x in ratvec_as_vec(v*simples) do 1-x%2 od { 1 for new rho_im }
  then sol = vals*mod2_section(simples)
  in assert ((sol*simples)%2 = vals,"no solution mod 2"); sol

set y_alt(Param p, InnerClass dual_int_ic) = KGBElt:
  let x=x(p), dual_tf=infinitesimal_character(p) - lambda(p)
  then dual_theta = -^involution(x)  { |dual_tf| is |dual_theta|-stable }
  then dual_x = { elt that just represents |dual_theta| in full dual group }
    KGB_elt(dual(inner_class(x)),dual_theta,null(#dual_tf))
  then rho_corr { correct changing of rho_check implicit in dual_tf }
    = theta_stable_diff(dual_x,dual_tf)
  , (simple_reals,)= simple_from_positive(imaginary_sys(dual_int_ic,dual_theta))
  then phi = dual_tf + rho_corr { dual_theta stable rep.ve of dual square }
  in prints(dual_tf,rho_corr,phi); KGB_elt(dual_int_ic,dual_theta
            ,phi+integral_shift(rho_corr-rho_r(x),simple_reals))


{ caution: these two functions build an inner class on the fly; if called
  mutliple times even with the same argument, the results will be incompatible
}
set y_alt(Param p) = KGBElt: y_alt(p,dual_inner_class(p))
set dual_real_form (Param p) = RealForm: real_form(y_alt(p))

{ cocharacter for (dual) real_form of y(p); should be constant %1 across block }
set character (Param p) = ratvec:
  let G=real_form(p), x=x(p)
  then int_d = integrality_datum(G,infinitesimal_character(p))
  then rho_r_int = sum(real_posroots(int_d,involution(x)))/2
  in ( infinitesimal_character(p) - lambda(p) + rho_r(x) { grading weight(x) }
      -rho_r_int + rho(int_d) )%1   { convert to cocharacter g(real_form(y)) }

set central_cocharacter (RealForm G) = ratvec:
  (base_grading_vector(G) + rho_check(G))%1

{ amount added to central_cocharacter(G) to get dual infinitesimal character }
set dual_shift (RootDatum rd, ratvec gamma) = ratvec:
  rho_check(integrality_datum(rd,gamma))-rho_check(rd)

{ Finding the value lambda for the dual parameter to p is surprisingly simple.
  Since imaginary roots are automatically integral, there is no difference
  of rho_i values to compensate for. The dual infinitesimal character is
  central_cocharacter(G)+dual_shift(G,gamma), but here rho_check(G) cancels
  out, giving base_grading_vector(G) + rho_check(integrality_datum(p)).
}

 { integral coweight associated to KGB element: is lambda-rho on dual side }
set ell(KGBElt x) = vec:
  let (numer,denom) = %(base_grading_vector(real_form(x))-torus_factor(x))
  in if denom=1 then numer else error("non-integral ell(x)") fi

set dual_gamma (Param p) = ratvec: { we want this to be strictly dominant }
  let G = real_form(p) in
  shift_g_regular(G,base_grading_vector(G) + rho_check(integrality_datum(p)))

set dual_lambda (Param p) = ratvec: { is mod 2: ell(p)+rho(dual_inner_class) }
  dual_gamma(p) - torus_factor(x(p))

set check_dual_lambda (Param p) = bool:
  dual_lambda(p)%1 = rho_check(integrality_datum(p))%1

set dual_alt (Param p, InnerClass dual_integral_class) = Param:
  parameter( y_alt(p,dual_integral_class), dual_lambda(p), dual_gamma(p) )

set dual_alt(Param p)=Param:dual_alt(p,dual_inner_class(p))

set test1 (Param p) = [bool]:
  let G_v=dual_inner_class(p) then drf = real_form(y_alt(p,G_v))
  in for q in block_of(p) do drf=real_form(y_alt(q,G_v)) od

set test2 (Param p) = [ratvec]:
  let G_v=dual_inner_class(p) then drf = real_form(y_alt(p,G_v))
  in for q in block_of(p) do character(q) od { should be constant gamma%1 }
